apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

repositories {
    mavenLocal()
    mavenCentral()
    google()
}

//noinspection UnnecessaryQualifiedReference
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

android {

    compileSdkVersion gradle.android_target_sdk_version
    buildToolsVersion gradle.android_build_tools_version
    defaultConfig {
        applicationId "com.splendo.kaluga.example"
        minSdkVersion gradle.android_min_sdk_version
        targetSdkVersion gradle.android_target_sdk_version
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            keyAlias "key0"
            keyPassword "nckI1UYofHIMkOnXpmZJVA"
            storeFile file("../keystore/debug.keystore")
            storePassword "nckI1UYofHIMkOnXpmZJVA"
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/kotlinx-coroutines-core.kotlin_module'
        exclude 'META-INF/shared_debug.kotlin_module'
        exclude 'META-INF/kotlinx-serialization-runtime.kotlin_module'
    }

    buildFeatures.dataBinding = true
}

gradle.ext.component_type = gradle.ext.component_type_composeApp

apply from: "../../gradle/android_compose.gradle"

gradle.ext.component_type = gradle.ext.component_type_default

dependencies {

    if (gradle.ext.exampleAsRoot) {
        // if the example is the root we use a maven dependency instead of the project
        implementation "com.splendo.kaluga:location:$gradle.ext.library_version"
        implementation "com.splendo.kaluga:permissions:$gradle.ext.library_version"
        implementation "com.splendo.kaluga:base:$gradle.ext.library_version"
        implementation "com.splendo.kaluga:date-time-picker:$gradle.ext.library_version"
        implementation "com.splendo.kaluga:logging:$gradle.ext.library_version"
        implementation "com.splendo.kaluga:alerts:$gradle.ext.library_version"
        implementation "com.splendo.kaluga:hud:$gradle.ext.library_version"
        implementation "com.splendo.kaluga:architecture:$gradle.ext.library_version"
        implementation "com.splendo.kaluga:architecture-compose:$gradle.ext.library_version"
        implementation "com.splendo.kaluga:keyboard:$gradle.ext.library_version"
        implementation "com.splendo.kaluga:links:$gradle.ext.library_version"
        implementation "com.splendo.kaluga:review:$gradle.ext.library_version"
        implementation "com.splendo.kaluga:system:$gradle.ext.library_version"
        implementation project(':shared')
    }
    else {
        implementation project(':location')
        implementation project(':base')
        implementation project(':date-time-picker')
        implementation project(':shared')
        implementation project(":permissions")
        implementation project(":logging")
        implementation project(":alerts")
        implementation project(":hud")
        implementation project(":architecture")
        implementation project(":architecture-compose")
        implementation project(":keyboard")
        implementation project(":links")
        implementation project(":review")
        implementation project(":system")
    }

    implementation "androidx.fragment:fragment-ktx:$gradle.androidx_fragment_version"
    implementation "com.google.android.gms:play-services-location:$gradle.play_services_version"
    implementation 'com.google.android.material:material:1.2.0-alpha06'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation "io.insert-koin:koin-android:$gradle.koin_version"
    implementation "androidx.lifecycle:lifecycle-service:$gradle.androidx_lifecycle_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$gradle.kotlinx_coroutines_version!!"

}
