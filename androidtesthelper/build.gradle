/*
 Copyright 2020 Splendo Consulting B.V. The Netherlands
 
    Licensed under the Apache License, Version 2.0 (the "License");
    you may not use this file except in compliance with the License.
    You may obtain a copy of the License at
 
      http://www.apache.org/licenses/LICENSE-2.0
 
    Unless required by applicable law or agreed to in writing, software
    distributed under the License is distributed on an "AS IS" BASIS,
    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
    See the License for the specific language governing permissions and
    limitations under the License.
  
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'


android {
    compileSdkVersion gradle.android_target_sdk_version
    buildToolsVersion gradle.android_build_tools_version

    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += ['-XXLanguage:+InlineClasses']
    }

    defaultConfig {
        applicationId "com.splendo.kaluga.androidtesthelper"
        minSdkVersion gradle.android_min_sdk_version
        targetSdkVersion gradle.android_target_sdk_version
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/kotlinx-coroutines-core.kotlin_module'
        exclude 'META-INF/shared_debug.kotlin_module'
        exclude 'META-INF/hud_debug.kotlin_module'
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
    }

    lintOptions {
        disable "InvalidPackage" // https://github.com/Kotlin/kotlinx.coroutines/issues/2004
    }


}

apply from: "../gradle/android_dependencies.gradle"

dependencies {

    implementation("androidx.fragment:fragment:$gradle.androidx_fragment_version")
    androidTestImplementation("androidx.fragment:fragment-ktx:$gradle.androidx_fragment_version")

    // Stately Isolite is in flux and not part of the current statelyVersion. Upgrade this when tracked properly
    androidTestImplementation("co.touchlab:stately-isolate:$gradle.stately_isolate_version")
    androidTestImplementation("co.touchlab:stately-iso-collections:$gradle.stately_isolate_version")

    def excluded = ["androidtesthelper", // this project
                    "logging",  // can not initialize after other projects
                    "location"] // in rework at the moment
    rootProject.subprojects.findAll{ !excluded.contains(it.name) }.each {
        implementation project(path: ":$it.name")
        androidTestImplementation project(path: ":$it.name", configuration:"androidTestHelperDebugImplementation")
    }

}